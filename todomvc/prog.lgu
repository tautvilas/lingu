#app item statusFilter statusType
#item title status
#statusType title value

on everyEvent
  context app

on firstRun
  add item
    with title todo.first.item
    with status doing
  add statusType with title status.all with value all
  add statusType with title status.active with value doing
  add statusType with title status.completed with value completed
  update app set statusFilter all

on enter titleInput
  if not empty titleInput
    add item with title from titleInput with status doing
    then focus titleInput and clear titleInput

on click itemDeletionButton
  remove selected

on click itemCompletionButton
  if is selectedStatus completed
    update selected set status doing
  else
    update selected set status completed

on doubleClick itemText
  show editField for selected
  hide view for selected
  and focus editField for selected

on escape editField
  hide editField for selected
  show view for selected
  setInputValue editField for selected valueOf selectedTitle

on enter editField , blur editField ,
  hide editField for selected
  show view for selected
  if empty editField for selected remove selected
  else update selected set title from editField for selected

on click toggleAllStatus
  if has doingItems
    update allItems set status completed
  else
    update allItems set status doing

on click clearButton
  remove completedItems

on click filterButton
  update app set statusFilter from target

on change item
  if has item
    show bodySection
  else hide bodySection .
  if has completedItems
    show clearButton
  else hide clearButton

*selectedStatus selected status
*selectedTitle selected title
*completedItems app item where status completed
*doingItems app item where status doing
*allItems app item

~todo.first.item This is your first task
~status.all All
~status.active Active
~status.completed Completed

